plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.diffplug.spotless" version "6.25.0"

    id 'maven-publish'
    id 'cl.franciscosolis.sonatype-central-upload' version '1.0.3'
}

group = 'io.github.suzumiyaaoba'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {    
    api "org.jspecify:jspecify:1.0.0"
    
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
    
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    toolVersion = '10.16.0'
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()

        googleJavaFormat()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                name = 'colors4j'
                description = 'An implementation of the picocolors in Java.'
                url = 'https://github.com/SuzumiyaAoba/colors4j'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'ISC License'
                        url = 'https://opensource.org/license/isc-license-txt'
                    }
                }

                scm {
                    url = 'https://github.com/SuzumiyaAoba/colors4j'
                    connection = 'scm:git://github.com:SuzumiyaAoba/colors4j.git'
                    developerConnection = 'scm:git://github.com:SuzumiyaAoba/colors4j.git'
                }

                developers {
                    developer {
                        id = 'SuzumiyaAoba'
                        name = 'Aoba Suzumiya'
                        email = 'SuzumiyaAoba@gmail.com'
                        organizationUrl = 'https://SuzumiyaAoba.github.io/'
                    }
                }
            }
        }
    }
}

sonatypeCentralUpload {
    dependsOn("jar", "sourcesJar", "javadocJar", "generatePomFileForMavenJavaPublication")

    username = findProperty("sonatypeUsername")
    password = findProperty("sonatypePassword")

    archives = files(
            tasks.named("jar"),
            tasks.named("sourcesJar"),
            tasks.named("javadocJar")
    )

    pom = file(
            tasks.named("generatePomFileForMavenJavaPublication").get().outputs.files.singleFile
    )

    signingKey = findProperty("signingKey")
    signingKeyPassphrase = findProperty("signingPassword")
}
