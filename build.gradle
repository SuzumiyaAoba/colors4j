plugins {
    id 'java'
    id 'checkstyle'
    id "com.diffplug.spotless" version "6.25.0"

    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'io.github.suzumiyaaoba'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '10.16.0'
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()

        googleJavaFormat()
    }
}

nexusPublishing {
    repositories {
        sonatype {
            //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
        }
    }
}


publishing {

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                name = 'colors4j'
                description = ''
                url = 'https://github.com/SuzumiyaAoba/colors4j'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'ISC License'
                        url = 'https://opensource.org/license/isc-license-txt'
                    }
                }

                scm {
                    url = 'https://github.com/SuzumiyaAoba/colors4j'
                    connection = 'scm:git://github.com:SuzumiyaAoba/colors4j.git'
                    developerConnection = 'scm:git://github.com:SuzumiyaAoba/colors4j.git'
                }

                developers {
                    developer {
                        id = 'SuzumiyaAoba'
                        name = 'Aoba Suzumiya'
                        email = 'SuzumiyaAoba@gmail.com'
                        organizationUrl = 'https://SuzumiyaAoba.github.io/'
                    }
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}